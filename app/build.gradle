apply plugin: 'com.android.application'
def AAVersion = '4.4.0'
def SupportLibVersion = '28.0.0'

configurations.all {
    resolutionStrategy {
        force "com.android.support:appcompat-v7:${SupportLibVersion}"
        force "com.android.support:support-v4:${SupportLibVersion}"
    }
}

android {
    compileSdkVersion versions.compile
    buildToolsVersion versions.buildTool
    defaultConfig {
        applicationId "org.autojs.autojs"
        minSdkVersion versions.mini
        targetSdkVersion versions.target
        versionCode versions.appVersionCode
        versionName versions.appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }
    }
    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding = 'utf-8'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}.apk"
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    flavorDimensions "channel"

    productFlavors {
        common {
            buildConfigField "String", "CHANNEL", '"common"'
        }
        coolapk {
            buildConfigField "String", "CHANNEL", '"coolapk"'
        }
    }

}




repositories {
    flatDir {
        dirs 'libs'
    }
    google()
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    // Android Annotations
    annotationProcessor "org.androidannotations:androidannotations:$AAVersion"
    implementation "org.androidannotations:androidannotations-api:$AAVersion"
    // ButterKnife
    implementation('com.jakewharton:butterknife:8.8.1', {
        exclude group: 'com.android.support'
    })
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    // Android supports
    implementation "com.android.support:appcompat-v7:${SupportLibVersion}"
    implementation "com.android.support:cardview-v7:${SupportLibVersion}"
    implementation "com.android.support:design:${SupportLibVersion}"
    implementation 'com.android.support:multidex:1.0.3'
    // Personal libraries
    implementation 'com.github.hyb1996:MutableTheme:0.2.2'
    // Material Dialogs
    implementation('com.afollestad.material-dialogs:core:0.9.2.3', {
        exclude group: 'com.android.support'
    })
    // Common Markdown
    implementation 'com.github.atlassian:commonmark-java:commonmark-parent-0.9.0'
    // Android issue reporter (a github issue reporter)
    implementation('com.heinrichreimersoftware:android-issue-reporter:1.3.1', {
        exclude group: 'com.afollestad.material-dialogs'
        exclude group: 'com.android.support'
    })
    //MultiLevelListView
    implementation 'com.github.hyb1996:android-multi-level-listview:1.1'
    //Licenses Dialog
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    //Expandable RecyclerView
    implementation 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'
    //FlexibleDivider
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    //???
    implementation 'com.wang.avi:library:2.1.3'
    //Commons-lang
    implementation 'org.apache.commons:commons-lang3:3.6'
    //Expandable RecyclerView
    implementation 'com.thoughtbot:expandablerecyclerview:1.3'
    implementation 'com.github.hyb1996:Auto.js-ApkBuilder:1.0.1'
    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.1.2"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    //Glide
    implementation('com.github.bumptech.glide:glide:4.8.0', {
        exclude group: 'com.android.support'
    })
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    //joda time
    implementation 'joda-time:joda-time:2.9.9'
    // Tasker Plugin
    implementation 'com.twofortyfouram:android-plugin-client-sdk-for-locale:4.0.3'
    // Flurry
    implementation 'com.flurry.android:analytics:7.0.0@aar'
    // Bugly
    implementation 'com.tencent.bugly:crashreport:2.6.6'
    // MaterialDialogCommon
    implementation('com.afollestad.material-dialogs:commons:0.9.2.3', {
        exclude group: 'com.android.support'
    })
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'
    implementation project(':automator')
    implementation project(':common')
    implementation project(':autojs')
    implementation (name: 'ads8', ext: 'aar')
}